let
    WsRaw   = CoLabEndpoint("workspaces", false),
    IdCol   = if Table.HasColumns(WsRaw,"id") then "id" else if Table.HasColumns(WsRaw,"workspaceId") then "workspaceId" else error "No workspace id column.",
    WsKeep0 = Table.SelectColumns(WsRaw, {IdCol, "name"}, MissingField.Ignore),
    WsKeep1 = if IdCol <> "id" then Table.RenameColumns(WsKeep0, {{IdCol,"id"}}) else WsKeep0,
    Ws      = Table.TransformColumns(WsKeep1, {{"id", each Text.From(_), type text}}),
    WsBuf   = Table.Buffer(Ws),

    // fnWsUsers returns {user: record, workspace_role: text} â€” no workspace_id stamp
    WithMembers          = Table.AddColumn(WsBuf, "members", each fnWsUsers([id])),
    #"Expanded members"  = Table.ExpandTableColumn(WithMembers, "members", {"user", "workspace_role"}, {"user", "workspace_role"}),
    #"Renamed Columns"   = Table.RenameColumns(#"Expanded members", {{"id", "workspace_id"}, {"name", "workspace_name"}}),
    #"Expanded user"     = Table.ExpandRecordColumn(#"Renamed Columns", "user", {"email", "id"}, {"email", "id"}),
    #"Renamed Columns1"  = Table.RenameColumns(#"Expanded user", {{"id", "user_id"}}),
    #"Changed Type"      = Table.TransformColumnTypes(#"Renamed Columns1", {
                               {"workspace_id",   Int64.Type},
                               {"workspace_name", type text},
                               {"email",          type text},
                               {"user_id",        Int64.Type},
                               {"workspace_role", type text}
                           }),
    Deduped = Table.Distinct(#"Changed Type", {"workspace_id", "user_id"})
in
    Deduped
